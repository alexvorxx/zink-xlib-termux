# An example meson.build for .rd decompiled into C source via rddecompiler.
# It uses Mesa as a subproject since the generated sources depend on
# generated headers with registers info and on some utils.
# Before compiling, create "subprojects" directory and create a link
# to mesa there.

project('generate_rd', ['c'], default_options : ['c_std=c11'])

mesa_proj = subproject('mesa',
                       default_options : ['gallium-drivers=',
                                          'dri-drivers=',
                                          'glx=disabled',
                                          'vulkan-drivers=',
                                          'tools=freedreno'])

add_project_arguments(mesa_proj.get_variable('pre_args'), language : ['c'])
add_project_arguments(mesa_proj.get_variable('c_args'),   language : ['c'])

generate_rd = executable(
  'generate_rd',
  [
    'generate_rd.c'
  ],
  include_directories: [
    mesa_proj.get_variable('inc_freedreno'),
    mesa_proj.get_variable('inc_freedreno_rnn'),
    mesa_proj.get_variable('inc_include'),
    mesa_proj.get_variable('inc_src'),
  ],
  link_with: [
    mesa_proj.get_variable('libfreedreno_cffdec'),
  ],
  dependencies : [
    mesa_proj.get_variable('idep_nir'),
  ],
)